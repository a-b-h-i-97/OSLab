alias Cause S0;
Cause = EFR%10;	

alias BadVAddr S1;
BadVAddr = (EFR/10)%100;

alias Ip_addr S2;
Ip_addr = (EFR/1000);

alias VPagenum S3;		
VPagenum = [PTBR + 2*BadVAddr];

alias physicalSP S4;
physicalSP=([PTBR + 2*(SP/512)]*512) + (SP%512);

if (Cause != 0) then
	if (Cause == 1) then
		print("Illegal instruction");
	endif;
	if (Cause == 2) then
		print("Illegal memory access");
	endif;
	if (Cause == 3) then
		print("Arithmetic exception");
	endif;
	if (Cause == 4) then
		print("Illegal operands");
	endif;
	SP=SP+2;
	[physicalSP + 1]=10;
	inline "JMP 11776";
endif;


if (VPagenum == -1) then
	SP=SP+2;
	[physicalSP + 1]=10;
	inline "JMP 11776";
endif;


alias counter S6;
counter =0;

while (counter < 64) do
	if ([MEM_LIST + counter] == 0) then
		[MEM_LIST + counter]=1;
		break;
	endif;
	counter=counter+1;
endwhile;

alias pageIndex S7;
pageIndex=PTBR - PAGE_TABLE + 2*BadVAddr + 2;

alias blockno S8;

if (counter == 64) then
	while(1) do
		if (pageIndex >= 256) then
			pageIndex=pageIndex-256;
		endif;

		if ((pageIndex % 8) == 6) then
			pageIndex=pageIndex+2;
			continue;
		endif;

		if ([PAGE_TABLE + pageIndex + 1] == "01") then
			if ([MEM_LIST + [PAGE_TABLE + pageIndex]] > 1) then
				pageIndex=pageIndex+2;
				continue;
			endif;

			blockno=448;
			while(blockno < 512) do
				if ([DISK_LIST + blockno] == 0) then
					break;
				endif;
				blockno=blockno+1;
			endwhile;

			if (blockno == 512) then
				SP=SP+2;
				[physicalSP + 1]=10;
				inline "JMP 11776";
			endif;

			store([PAGE_TABLE + pageIndex],blockno);
			[DISK_LIST + blockno]=1;
			store(6,20);

			counter=[PAGE_TABLE + pageIndex];
			[PAGE_TABLE + pageIndex]=blockno;
			[PAGE_TABLE + pageIndex + 1]="00";
			break;
		endif;

		if ([PAGE_TABLE + pageIndex + 1] == "11") then
			[PAGE_TABLE + pageIndex + 1]="01";
		endif;

		pageIndex=pageIndex+2;
	endwhile;
endif;

if ((VPagenum > 20) && (VPagenum < 448)) then
	load(counter, VPagenum);
endif;

if (VPagenum >= 448) then
	load(counter, VPagenum);
	[DISK_LIST + VPagenum]=[DISK_LIST + VPagenum]-1;
	store(6,20);
endif;

[PTBR + 2*BadVAddr]=counter;
[PTBR + 2*BadVAddr + 1]="11";

SP=SP+1;
[physicalSP + 1]=Ip_addr;

ireturn;
